apply plugin: 'com.android.application'

def releaseSigningProperties = "${System.env["HOME"]}/.gradle/signing/release.properties"

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.2"

    defaultConfig {
        applicationId "org.hogel.android.simplepainting"
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 1
        versionName "0.0.1"
    }

    signingConfigs {
        release {
            if (new File(releaseSigningProperties).exists()) {
                Properties props = new Properties()
                props.load(new FileInputStream(file(releaseSigningProperties)))
                storeFile file(props['STORE_FILE'])
                storePassword props['STORE_PASSWORD']
                keyAlias props['KEY_ALIAS']
                keyPassword props['KEY_PASSWORD']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.jakewharton:butterknife:6.0.0'
    compile 'com.github.frankiesardo:icepick:2.3.6'
    compile 'org.slf4j:slf4j-android:1.7.7'
    compile 'com.google.guava:guava:18.0'

    provided 'org.projectlombok:lombok:1.14.8'
    provided 'com.github.frankiesardo:icepick-processor:2.3.6'
}
